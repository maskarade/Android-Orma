import java.util.regex.Pattern

apply from: project.file('metadata.gradle')

final ANDROID_SDK_PATH = {
    final localProperties = new Properties()
    localProperties.load(new FileInputStream("${project.rootProject.projectDir}/local.properties"))
    def androidSdkPath = localProperties['sdk.dir'] ?: System.getenv('ANDROID_HOME')
    if (!androidSdkPath) {
        throw new RuntimeException("Missing local.properties")
    }
    androidSdkPath
}()
final ANDROID_SUPPORT_REPOSITORY = "${ANDROID_SDK_PATH}/extras/android/m2repository"

ext {
    SUPPORT_LIBRARY_VERSION = '23.4.0'
    ANDROID_JAR_23 = fileTree(dir: "${ANDROID_SDK_PATH}/platforms/android-23/", include: 'android.jar')

    versionFile = project.file("version.properties")

    versionName = {
        def versionProps = getVersionProps()
        "${versionProps['major']}.${versionProps['minor']}.${versionProps['patch']}"
    }

    metadata.version = versionName()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.novoda:bintray-release:0.3.4' // https://github.com/novoda/bintray-release
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // it conflicts with retrolambda
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        maven {
            url ANDROID_SUPPORT_REPOSITORY
        }

        jcenter()
    }

    gradle.projectsEvaluated {
        if (project.name == "example") {
            return
        }

        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Xlint:-processing"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def updateReadme(oldVersion, newVersion) {
    def template = file('README.md').text
    def result = template.replaceAll(Pattern.quote(oldVersion), newVersion)
    file("README.md").withWriter { it << result }
}

def Properties getVersionProps() {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionFile))
    return versionProps
}

task bumpMajor << {
    def oldVersion = versionName()
    ant.propertyfile(file: versionFile) {
        entry(key: 'major', type: 'int', operation: '+', value: 1)
        entry(key: 'minor', type: 'int', operation: '=', value: 0)
        entry(key: 'patch', type: 'int', operation: '=', value: 0)
    }
    updateReadme(oldVersion, versionName())
    tasks.version.execute()
}

task bumpMinor << {
    def oldVersion = versionName()
    ant.propertyfile(file: versionFile) {
        entry(key: 'minor', type: 'int', operation: '+', value: 1)
        entry(key: 'patch', type: 'int', operation: '=', value: 0)
    }
    updateReadme(oldVersion, versionName())
    tasks.version.execute()
}

task bumpPatch << {
    def oldVersion = versionName()
    ant.propertyfile(file: versionFile) {
        entry(key: 'patch', type: 'int', operation: '+', value: 1)
    }
    updateReadme(oldVersion, versionName())
    tasks.version.execute()
}

task version << {
    println "v" + versionName()
}

String shell(String command) {
    def proc = ["sh", "-c", "cd ${project.rootDir} ; ${command}"].execute()
    if (proc.waitFor() != 0) {
        throw new RuntimeException("Failed to run: ${command}\n${proc.err.text}")
    } else {
        def err = proc.err.text
        if (err) {
            System.err.println(err)
        }
    }
    return proc.in.text;
}

task releng << {
    def tag = "v" + versionName()
    println "Release engineering for ${tag}"

    def changes = shell "git status -s"
    if (changes.trim()) {
        throw new RuntimeException("There are changes not commited yet.\n${changes}")
    }

    println "> git tag ${tag}"
    shell "git tag ${tag}"
    println "> git push origin ${tag}"
    shell "git push origin ${tag}"
    shell "git push origin master"
}
