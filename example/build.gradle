buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'io.realm:realm-gradle-plugin:3.7.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.github.gfx.android.orma.example"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion SDK_VERSION
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "Orma ${rootProject.ext.versionName} (release)"
        }
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "Orma ${rootProject.ext.versionName} (release)"
        }
    }

    flavorDimensions "app"

    productFlavors {
        normal {
            dimension "app"
        }
        encrypted {
            dimension "app"

            applicationIdSuffix ".encrypted"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        disable 'SetTextI18n'
        disable 'UnusedResources'
        disable 'HardcodedText'
        disable 'RtlSymmetry'
        disable 'RtlHardcoded'
        disable 'IconLocation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    dexOptions {
        preDexLibraries = !System.getenv("CI")
    }
}

dependencies {
    annotationProcessor project(':processor')
    implementation project(':core')
    implementation project(':library')
    implementation project(':encryption')
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}
